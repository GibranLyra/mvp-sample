apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'me.tatarka:gradle-retrolambda:3.6.1'
    }
}

// Required because retrolambda is on maven central
repositories {
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
}

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'
    defaultConfig {
        applicationId "gibran.com.br.mvpsample"
        minSdkVersion 19
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        flavorDimensions "default"
        // Enabling multidex support.
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        devTest {
            applicationIdSuffix = ".devTest"
            resValue "string", "buildType", "devTest"
        }
        prod {
            applicationId "br.com.net.nowkids.dev"
            resValue "string", "buildType", "prod"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'scala-asm.properties'
    }
}

ext {
    supportVersion = '25.4.0'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.android.support:appcompat-v7:${supportVersion}"
    compile "com.android.support:design:${supportVersion}"
    compile "com.android.support:support-v4:${supportVersion}"
    compile "com.android.support:cardview-v7:${supportVersion}"
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    //Material Dialogs
    compile 'com.afollestad.material-dialogs:core:0.9.4.5'
    //ButterKnife
    compile 'com.jakewharton:butterknife:8.6.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.6.0'
    //RX
    compile "io.reactivex.rxjava2:rxjava:2.1.0"
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    //Timber
    compile 'com.jakewharton.timber:timber:4.5.1'
    //Picasso
    compile 'com.github.bumptech.glide:glide:3.7.0'
    //FastAdapter
    compile('com.mikepenz:fastadapter:2.6.2@aar') {
        transitive = true
    }
    compile 'com.mikepenz:fastadapter-extensions:2.6.3@aar'
    //Material Drawer
    compile('com.mikepenz:materialdrawer:5.9.2@aar') {
        transitive = true
    }
    compile 'com.mikepenz:fastadapter-commons:2.6.0@aar'
    // JodaTime
    compile 'net.danlew:android.joda:2.9.5'
    //Timezone
    compile 'com.jakewharton.threetenabp:threetenabp:1.0.5'
    compile project(path: ':dribbleservice')
    //Guava
    compile "com.google.guava:guava:$rootProject.guavaVersion"
    // Dependencies for local unit tests
    testCompile "junit:junit:$rootProject.ext.junitVersion"
    testCompile "org.mockito:mockito-all:$rootProject.ext.mockitoVersion"
    testCompile "org.hamcrest:hamcrest-all:$rootProject.ext.hamcrestVersion"
    testCompile "org.powermock:powermock-module-junit4:$rootProject.ext.powerMockito"
    testCompile "org.powermock:powermock-api-mockito:$rootProject.ext.powerMockito"
    // Android Testing Support Library's runner and rules
    androidTestCompile "com.android.support.test:runner:$rootProject.ext.runnerVersion"
    androidTestCompile "com.android.support.test:rules:$rootProject.ext.rulesVersion"

    // Espresso UI Testing dependencies.
    androidTestCompile "com.android.support.test.espresso:espresso-core:$rootProject.ext.espressoVersion"
    androidTestCompile "com.android.support.test.espresso:espresso-contrib:$rootProject.ext.espressoVersion"
    androidTestCompile "com.android.support:support-annotations:$supportVersion"
    androidTestCompile "com.android.support.test.espresso.idling:idling-concurrent:$rootProject.espressoVersion"
    androidTestCompile "com.android.support.test.espresso:espresso-intents:$rootProject.espressoVersion"
    compile "com.android.support.test.espresso:espresso-idling-resource:$rootProject.ext.espressoVersion"
    //MultiDex
    compile 'com.android.support:multidex:1.0.1'
}

String java7 = getJavaVersion(7)
String java8 = getJavaVersion(8)

retrolambda {
    jdk java8
    oldJdk java7
    javaVersion JavaVersion.VERSION_1_7
    defaultMethods false
    incremental true
}

String getJavaVersion(Integer v) {
    def envVar = v == 7 ? System.getenv("JAVA_HOME") : System.getenv("JAVA8_HOME")
    if (envVar != null && envVar.trim() != "") {
        return envVar
    }
    def sout = new StringBuffer()
    def proc = "/usr/libexec/java_home -v 1.$v".execute()
    proc.consumeProcessOutput(sout, new StringBuffer())
    proc.waitForOrKill(1000)
    def javaHome = sout.toString().replace("\n", "").replace("\r", "")
    return javaHome
}